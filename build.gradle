plugins {
    id 'java'
    id 'antlr'
    id 'checkstyle'
}

group 'org.quiltmc'
version '1.0-SNAPSHOT'

sourceSets {
    testData {
        java {
            srcDirs file("src/testData/java")
            compileClasspath = sourceSets.main.output + configurations.testCompileClasspath
            runtimeClasspath = sourceSets.main.output + project.configurations.testRuntimeClasspath
        }
    }
}

compileJava {
    javaCompiler = javaToolchains.compilerFor {
        languageVersion = JavaLanguageVersion.of(8)
    }
}

compileTestJava {
    javaCompiler = javaToolchains.compilerFor {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

compileTestDataJava {
    javaCompiler = javaToolchains.compilerFor {
        languageVersion = JavaLanguageVersion.of(17)
    }
    destinationDirectory.set(file("src/testData/classes"))
}

testClasses.dependsOn("testDataClasses")

checkstyle {
    toolVersion "9.1"
    ignoreFailures(false)
}

generateGrammarSource {
    maxHeapSize = "64m"
    arguments += ["-visitor", "-long-messages"]
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.ow2.asm:asm:9.2'
    antlr 'org.antlr:antlr4:4.9.3'

    compileOnly 'org.jetbrains:annotations:23.0.0'

    testImplementation 'org.junit.jupiter:junit-jupiter:5.8.1'
    testImplementation 'org.ow2.asm:asm-util:9.2'
    testImplementation 'com.googlecode.java-diff-utils:diffutils:1.3.0'
    testImplementation 'org.reflections:reflections:0.10.2'
}

test {
    useJUnitPlatform()
}
