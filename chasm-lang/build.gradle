plugins {
    id 'java'
    id 'antlr'
    id 'checkstyle'
    id 'maven-publish'
}

group 'org.quiltmc.chasm'
version '0.0.1'

compileJava {
    javaCompiler = javaToolchains.compilerFor {
        languageVersion = JavaLanguageVersion.of(8)
    }
}

compileTestJava {
    javaCompiler = javaToolchains.compilerFor {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

generateGrammarSource {
    maxHeapSize = "64m"
    arguments += ["-visitor", "-long-messages"]
}

checkstyle {
    toolVersion "9.1"
    ignoreFailures(false)
}

repositories {
    mavenCentral()
}

dependencies {
    implementation project(':chasm')
    implementation 'org.ow2.asm:asm:9.2'


    antlr 'org.antlr:antlr4:4.9.3'

    testImplementation 'org.ow2.asm:asm-util:9.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
}

test {
    useJUnitPlatform()
}

publishing {
    repositories {
        maven {
            name = 'quiltSnapshot'
            url = System.getenv('SNAPSHOTS_URL')

            credentials {
                username = System.getenv('SNAPSHOTS_USERNAME')
                password = System.getenv('SNAPSHOTS_PASSWORD')
            }
        }
    }

    publications {
        snapshot(MavenPublication) {
            version = project.version + "-SNAPSHOT"

            from components.java
        }
    }
}
